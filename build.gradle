buildscript {
	ext {
		kotlin_version = '1.2.51'
		junit_version = '5.2.0'
		jackson_version = '2.9.6'
		log4j2_version = '2.11.0'
		mockito_version = '2.19.0'
		vertx_version = '3.5.2'
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

subprojects {
	apply plugin: 'kotlin'
	apply plugin: 'jacoco'

	group = 'com.cryptax'
	version = '1.0.0'

	sourceCompatibility = JavaVersion.VERSION_1_10
	targetCompatibility = JavaVersion.VERSION_1_10

	repositories {
		jcenter()
	}

	compileJava {
		doFirst {
			options.compilerArgs = [
				'--module-path', classpath.asPath,
			]
			classpath = files()
		}
	}

	sourceSets {
		main {
			java {
				srcDirs = ['src/main/kotlin']
			}
		}
	}

	dependencies {
		implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
		implementation 'org.slf4j:slf4j-api:1.8.0-beta2'
		implementation "org.apache.logging.log4j:log4j-core:$log4j2_version"
		implementation "org.apache.logging.log4j:log4j-api:$log4j2_version"
		implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j2_version"

		testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
		testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_version"
		testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
	}

	ext {
		projectName = rootProject.name
	}


	test {
		useJUnitPlatform()

		testLogging {
			events 'PASSED', 'FAILED', 'SKIPPED'
		}

		reports {
			html.enabled = true
		}

	}

	compileKotlin {
		kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
		destinationDir = compileJava.destinationDir
	}

	compileTestKotlin {
		kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
	}

	clean.doFirst {
		delete 'out'
	}

	jacocoTestReport {
		reports {
			xml.setEnabled(false)
			csv.setEnabled(false)
		}

		afterEvaluate {
			classDirectories = files(classDirectories.files.collect {
				fileTree(dir: it, exclude: [])
			})
		}
	}
}

apply plugin: 'java'
apply plugin: 'jacoco'

repositories {
	jcenter()
}

ext {
	projectName = rootProject.name
}

jar.enabled = false

task testReport(type: TestReport) {
	destinationDir = file("$buildDir/reports/allTests")
	reportOn subprojects*.test
}

task jacocoRootReport(type: JacocoReport) {
	dependsOn = subprojects.test
	additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
	sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
	classDirectories = files(subprojects.sourceSets.main.output)
	executionData = files(subprojects.jacocoTestReport.executionData)
	reports {
		html.enabled = true
		xml.enabled = true
		csv.enabled = false
	}
	onlyIf = {
		true
	}
	doFirst {
		executionData = files(executionData.findAll {
			it.exists()
		})
	}
}
