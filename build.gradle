buildscript {
	ext {
		kotlin_version = '1.2.51'
		junit_version = '5.2.0'
		mockito_version = '2.19.0'
		jackson_version = '2.9.6'
		vertx_version = '3.5.2'
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'kotlin'

	group = 'com.cryptax'
	version = '1.0.0'

	sourceCompatibility = JavaVersion.VERSION_1_10
	targetCompatibility = JavaVersion.VERSION_1_10

	repositories {
		jcenter()
	}

	compileJava {
		doFirst {
			options.compilerArgs = [
				'--module-path', classpath.asPath,
			]
			classpath = files()
		}
	}

	sourceSets {
		main {
			java {
				srcDirs = ['src/main/kotlin']
			}
		}
	}

	dependencies {
		implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
		implementation 'org.slf4j:slf4j-api:1.8.0-beta2'
		implementation 'org.apache.logging.log4j:log4j-core:2.11.0'
		implementation 'org.apache.logging.log4j:log4j-api:2.11.0'
		implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.0'

		//implementation "org.slf4j:slf4j-log4j12:1.8.0-beta2"
		//implementation 'org.slf4j:slf4j-simple:1.8.0-beta2'
		//compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'
		//compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'

		testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
		testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_version"
		testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
	}

	test {
		useJUnitPlatform()

		testLogging {
			events 'PASSED', 'FAILED', 'SKIPPED'
		}

		reports {
			html.enabled = true
		}
	}

	compileKotlin {
		kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
		destinationDir = compileJava.destinationDir
	}
	compileTestKotlin {
		kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
	}

	clean.doFirst {
		delete 'out'
	}
}
